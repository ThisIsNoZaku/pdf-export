apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

repositories{
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile "org.apache.pdfbox:pdfbox:1.8.12"
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
	compile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'junit:junit:4.12'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	compile 'org.slf4j:slf4j-simple:1.7.28'
}

project.version = "1.1.1"

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	classifier = 'javadoc'
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "io.github.thisisnozaku"
			artifactId = "pdf-export"
			version = "${project.version}"
            from components.java
		}

		sonatype(MavenPublication) {
			groupId = "io.github.thisisnozaku"
			artifactId = "pdf-export"
			version = "${project.version}"
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}


signing {
	sign publishing.publications.sonatype
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

sourceSets{
	main {
		java{
			srcDir 'src/java'
		}
	}
}

jar {
    from sourceSets.main.output
	archiveName = "${project.name}-${project.version}.jar"
	manifest {
		attributes 'Main-Class': 'io.github.thisisnozaku.pdfexporter.Main'
	}

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

task wrapper(type: Wrapper){
	gradleVersion = '4.10'
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: project.properties["ossrhUsername"], password: project.properties["ossrhPassword"])
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: project.properties["ossrhUsername"], password: project.properties["ossrhPassword"])
			}

			pom.project {
				name 'Pdf-export'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'A Pdf-form filling application'
				url "https://github.com/ThisIsNoZaku/pdf-export"
				scm {
					url "https://github.com/ThisIsNoZaku/pdf-export.git"
				}


				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'damien'
						name 'Damien Marble'
						email 'damienmarble@gmail.com'
					}
				}
			}
		}
	}
}